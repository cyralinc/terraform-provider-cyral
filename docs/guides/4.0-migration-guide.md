---
page_title: "Cyral Terraform Provider V4 Migration Guide"
---

This guide will take you through the steps required to upgrade your Cyral Terraform provider
to MAJOR version 4. The Terraform migration can be performed on any Cyral Terraform provider with
version `2.x` or `3.x`.
The migration will be handled by an interactive Bash script.

# Why Migration is Required

Cyral Terraform Provider V4 Migration is required when upgrading a Cyral Control Plane (CP) to MAJOR version 4. Major version 4 introduces breaking changes to Cyral's repositories and repository bindings. It also introduces the concept of sidecar listeners and access gateways, which are now separate from repository bindings.

When the Cyral CP is upgraded to major version 4, the CP will automatically migrate these objects from the old
data model to the new data model, and create sidecar listener and access gateways appropriately. However, if you
are using Terraform to manage these resources, attempting to run `terraform plan` after the CP has been upgraded
will fail, due to breaking API changes and corresponding changes in the schema definitions for the resources.

In order to update your Cyral Terraform Provider V4, we have provided scripts that will remove all
`cyral_repository` and `cyral_repository_binding` resources from your Terraform state, and then import the newly
migrated versions from the Cyral CP. These scripts ensure that all resources are imported properly into your
Terraform state and configuration files.

## Resource changes

Cyral Terraform Provider V4 introduces [Smart Ports](https://cyral.com/docs/v4.0/sidecars/sidecar-bind-repo/#smart-ports), which let you configure multiple data repositories behind a single network port in the Cyral sidecar.

With the introduction of [Smart Ports](https://cyral.com/docs/v4.0/sidecars/sidecar-bind-repo/#smart-ports), it is now possible to define multiple listener configurations for a single sidecar. This feature required some breaking changes to the schema of [`cyral_repository`](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/resources/repository) and [`cyral_repository_binding`](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/resources/repository_binding) while also adding new resources. Here is the summary of the changes:

- New resources named [`cyral_sidecar_listener`](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/resources/sidecar_listener) and [`cyral_repository_access_gateway`](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/resources/repository_access_gateway) were introduced.
- Argument `cyral_repository.host` was moved to `cyral_repository.repo_node.host`.
- Argument `cyral_repository.port` was moved to `cyral_repository.repo_node.port`.
- Argument `cyral_repository.properties.mongodb_replica_set.max_nodes` was removed. The argument is no
  longer used and is automatically inferred from the number of `repo_node` blocks declared in
  `cyral_repository`.
- Argument `cyral_repository.properties.mongodb_replica_set.replica_set_id` was moved to `cyral_repository.mongodb_settings.replica_set_name`.
- Argument `cyral_repository_binding.listener_host` was moved to `cyral_sidecar_listener.network_address.host`.
- Argument `cyral_repository_binding.listener_port` was moved to `cyral_sidecar_listener.network_address.port`.
- Argument `cyral_repository_binding.sidecar_as_idp_access_gateway` was moved to the resource [`cyral_repository_access_gateway`](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/resources/repository_access_gateway). Please check the resource documentation.

Please see the example below of how resource definitions for `cyral_repository` and `cyral_repository_binding` resources would be rewritten upon upgrading to Cyral Terraform Provider V4.

Before upgrading to Cyral Terraform Provider V4, a configuration to bind a repository to a sidecar resource `cyral_sidecar.sidecar` that exposes a MongoDB instance that lives in port `27017` in the sidecar port `27020` would look something like this:

```terraform
resource "cyral_repository" "mongo_repo" {
  name = "mongo-repo"
  host = "mongodb.cyral.com"
  port = 27017  # This is the port in the database host
  type = "mongodb"
}

resource "cyral_repository_binding" "mongo_binding" {
  enabled                       = true
  repository_id                 = cyral_repository.mongo_repo.id
  sidecar_id                    = cyral_sidecar.sidecar.id
  listener_port                 = 27020  # This is the port the sidecar will expose
                                          # for users/applications connecting to the
                                          # database.
  sidecar_as_idp_access_gateway = true
}
```

After upgrade the resource definitions above would be rewritten as follows:

```terraform
resource "cyral_repository" "mongo_repo" {
  name = "mongo-repo"
  type = "mongodb"
  repo_node {
    host = "mongodb.cyral.com"
    port = 27017
  }
}

resource "cyral_repository_binding" "binding" {
  sidecar_id  = cyral_sidecar.sidecar.id
  repository_id = cyral_repository.mongo_repo.id
  enabled   = true
  listener_binding {
    listener_id = cyral_sidecar_listener.listener.listener_id
    node_index  = 0
  }
}

resource "cyral_sidecar_listener" "listener" {
  sidecar_id = cyral_sidecar.sidecar.id
  repo_types = ["mongodb"]
  network_address {
    port = 27020
  }
}

resource "cyral_repository_access_gateway" "access_gateway" {
  repository_id = cyral_repository.mongo_repo.id
  sidecar_id  = cyral_sidecar.sidecar.id
  binding_id  = cyral_repository_binding.binding.binding_id
}
```

## Data source changes

The `cyral_repository` data source output contains breaking changes. The new schema can be found [here](https://registry.terraform.io/providers/cyralinc/cyral/latest/docs/data-sources/repository). Please note that if you have any output blocks configured
for `cyral_repository` data sources, **you will need to rewrite them after migration is completed**.

---

# Migrating to Cyral Terraform provider 4.0

The following sections contain step-by-step instructions on how to migrate the Cyral Terraform Provider
to version 4.0 using a migration script created to facilitate the process.

## Prerequisites

The migration script requires the following tools:

- Bash Version 4 or higher. The script will check your `$BASH_VERSION` environment variable, and exit if it is not set to a version 4.0 or higher.

- Terraform CLI. Download instructions can be found [here](https://learn.hashicorp.com/tutorials/terraform/install-cli).

- JQ. Download instructions can be found [here](https://stedolan.github.io/jq/download/).

- Access configuration to your Cyral Control Plane set to the following environment variables before running the script:
  - `CYRAL_TF_CONTROL_PLANE` (`[tenant].app.cyral.com`)
  - `CYRAL_TF_CLIENT_ID`
  - `CYRAL_TF_CLIENT_SECRET`.

The script will exit if either of these tools are not installed or the environment variables are not set.

### Notes

1. The script requires permissions sufficient for creating files. Please ensure that the script has the required permissions before running it.

2. The script will **append** empty resource definitions to the end of your `.tf` file. Apart from that, it will not modify the resource definitions
   currently in your `.tf` file in any way. However, it will ask you to manually bump the Cyral Provider version halfway through the script.
   It will also ask you to manually remove both the empty resource definitions it appended to your `.tf` file, as well as all resource definitions
   for resources that are no longer supported.

3. Please carefully read all of the prompts that appear throughout the script.

---

## Migrating from 3.X to 4.0

The following steps should be taken to upgrade the Cyral CP and the Cyral Terraform provider:

1. **Before upgrading the CP to V4**, please run `terraform apply` to ensure your Terraform state is up-to-date.

2. Upgrade the Cyral CP to MAJOR version 4 (contact our Customer Success team to schedule it).

3. Remove all `cyral_repository` data source output blocks. If you have any output blocks configured
   for `cyral_repository` data sources, you will need to rewrite them based on the new schema after
   migration is completed.

4. Run the Cyral Terraform Provider [V4 Migration Script](https://github.com/cyralinc/terraform-provider-cyral/tree/main/scripts/4.0-migration.sh) after upgrading
   the Cyral CP to MAJOR version 4. Refer to the [Running the migration script](#running-the-migration-script) section of this page for more information.

~> **WARNING** It is essential that the Cyral Terraform Provider V4 Migration is run **after** the CP has been upgraded.

---

## Migrating from 2.X to 4.0

The following steps should be taken to upgrade the Cyral CP and the Cyral Terraform provider:

1. **Before upgrading the CP to V4**, please run `terraform apply` to ensure your Terraform state is up-to-date.

2. Upgrade the Cyral CP to MAJOR version 4.

3. Remove all `cyral_repository` data source output blocks. If you have any output blocks configured
   for `cyral_repository` data sources, you will need to rewrite them based on the new schema after
   migration is completed.

4. Run the Cyral Terraform Provider [V2-V4 Migration Script](https://github.com/cyralinc/terraform-provider-cyral/tree/main/scripts/2.X-4.0-migration.sh) after upgrading
   the Cyral CP to MAJOR version 4. Refer to the [Running the migration script](#running-the-migration-script) section of this page for more information.

~> **WARNING** It is essential that the Cyral Terraform Provider V2-V4 Migration is run **after** the CP has been upgraded.

---

## Running the migration script

Cyral Terraform Provider **V3 to V4 Migration** script can be found [here](https://github.com/cyralinc/terraform-provider-cyral/tree/main/scripts/4.0-migration.sh).

Cyral Terraform Provider **V2 to V4 Migration** script can be found [here](https://github.com/cyralinc/terraform-provider-cyral/tree/main/scripts/2.X-4.0-migration.sh).

These scripts will create a backup of your Terraform state before attempting to upgrade your Cyral Terraform provider and performing the migration.
If migration fails, you will have the option to revert to the Terraform state that was present before running the script, so that you can try again.

In order to run the script, please perform the following actions:

1.  Download the script and copy it into the directory containing the Terraform module you wish to migrate.

2.  Run the migration script (\*make sure the script has the required permissions as mentioned in the [requirements section](#requirements)). Please read all instructions carefully while interacting with the script.

3.  If migration failed, follow the prompts in the script to revert back to the previous state. Inspect the errors, and try again.

## Configuring resources defined with `for_each`

Using `terraform state list` might be helpfull to understand all the resources that are present in your terraform
state.

### `cyral_repository`

#### General Configuration

```terraform
locals {
  repos = {
    postgresql = {
      host  = "postgres.com"
      ports = [5432]
      type  = "postgresql"
    }
    # some other repos definitions...
  }
}

resource "cyral_repository" "all_repositories" {
  for_each = local.repos
  name  = each.key
  type  = each.value.type

  connection_draining {
    auto      = false
    wait_time = 0
  }

  dynamic "repo_node" {
      for_each = range(0, length(each.value.ports))
      content {
          dynamic = repo_node.value == 0 ? false : true
          host = repo_node.value == 0 ? each.value.host : ""
          port = repo_node.value == 0 ? each.value.ports[0] : 0
      }
  }
}
```

### MongoDB Configuration

```terraform
locals {
  repos = {
    mongodb = {
      host             = "mongodb.com"
      ports            = range(27017, 27020)
      type             = "mongodb"
      server_type      = "replicaset"
      replica_set_name = "replica-set-id-123"
    }
    # some other repos definitions...
  }
}

resource "cyral_repository" "all_repositories" {
  for_each = local.repos
  name  = each.key
  type  = each.value.type

  connection_draining {
    auto      = false
    wait_time = 0
  }

  dynamic "repo_node" {
      for_each = range(0, length(each.value.ports))
      content {
          dynamic = repo_node.value == 0 ? false : true
          host = repo_node.value == 0 ? each.value.host : ""
          port = repo_node.value == 0 ? each.value.ports[0] : 0
      }
  }

  dynamic "mongodb_settings" {
    for_each = each.value.type == "mongodb" ? (each.value.server_type == "replicaset" ? [""] : []) : []
    content {
      replica_set_name = each.value.replica_set_name
      server_type = each.value.server_type
    }
  }
}
```

### `cyral_sidecar_listener`

Define a local variable to map the different repo type and port combinations, similar to the following:

```terraform
locals {
  type_port_map = merge([
    for key, repo in local.repos : {
      for port in repo.ports :
      "${repo.type}_${port}" => {
          type = repo.type
          port = port
      }
    }
  ]...)
}
```

Then define the `cyral_sidecar_listener` resources using a for_each that iterates over the local variables:

#### MySQL, MariaDB and Galera Configuration

```terraform
resource "cyral_sidecar_listener" "sidecar_all_listeners" {
  for_each = local.type_port_map
  repo_types = [each.value.type]
  sidecar_id = cyral_sidecar.sidecar.id

  network_address {
    host = "0.0.0.0"
    port = each.value.port
  }

  dynamic "mysql_settings" {
    for_each = each.value.type == "mysql" ? [""] : []
    content {
      character_set = ""
      db_version = ""
    }
  }
}
```

#### DynamoDB Configuration

```terraform
resource "cyral_sidecar_listener" "sidecar_all_listeners" {
  for_each = local.type_port_map
  repo_types = [each.value.type]
  sidecar_id = cyral_sidecar.sidecar.id

  network_address {
    host = "0.0.0.0"
    port = each.value.port
  }

  dynamic "dynamodb_settings" {
    for_each = each.value.type == "dynamodb" ? [""] : []
    content {
      proxy_mode = true
    }
  }
}
```

#### S3 Configuration

```terraform
resource "cyral_sidecar_listener" "sidecar_all_listeners" {
  for_each = local.type_port_map
  repo_types = [each.value.type]
  sidecar_id = cyral_sidecar.sidecar.id

  network_address {
    host = "0.0.0.0"
    port = each.value.port
  }

  dynamic "s3_settings" {
    for_each = each.value.type == "s3" ? [""] : []
    content {
      proxy_mode = true
    }
  }
}
```

### `cyral_repository_binding`

```terraform
resource "cyral_repository_binding" "all_repo_binding" {
  for_each = local.repos
  enabled       = true
  repository_id = cyral_repository.all_repositories[each.key].id
  sidecar_id    = cyral_sidecar.sidecar.id

  dynamic "listener_binding" {
    for_each = each.value.ports
    content {
      listener_id = cyral_sidecar_listener.sidecar_all_listeners["${each.value.type}_${listener_binding.value}"].listener_id
      node_index = listener_binding.key
    }
  }
}
```

### `cyral_repository_access_gateway`

```terraform
resource "cyral_repository_access_gateway" "all_repo_binding_all_access_gateways" {
  for_each = local.repos
  repository_id  = cyral_repository.all_repositories[each.key].id
  sidecar_id  = cyral_sidecar.sidecar.id
  binding_id = cyral_repository_binding.all_repo_binding[each.key].binding_id
}
```

## Troubleshooting

If migration failed for one or two resources, do the following.

1.  Revert to the previous state by following the prompts at the end of the migration script.
2.  Manually remove the problematic resources from your .tf file, and copy them elsewhere.
3.  Run the migration script again.
4.  Once the script is finished, recreate the problematic resources using the new resource schema.
