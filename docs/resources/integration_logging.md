# cyral_integration_logging (Resource)

Manages a logging integration that can be used to push logs from Cyral to the corresponding logging system (E.g.: AWS CloudWatch, Splunk, SumoLogic, etc).

## Example Usage

See the list of all the supported logging integrations in the resource schema.
More information can be found in our [public docs](https://cyral.com/docs/v4.7/integrations/siem/overview).

```terraform
# Configures `my-sidecar-cloudwatch` to push logs to CloudWatch to a log
# group named `cyral-example-loggroup` and a stream named `cyral-sidecar`.
locals {
  cloudwatch_log_group_name = "cyral-example-loggroup"
}

resource "cyral_sidecar" "sidecar" {
  name = "my-sidecar-cloudwatch"
  deployment_method = "terraform"
  log_integration_id = cyral_integration_logging.cloudwatch.id
}

resource "cyral_integration_logging" "cloudwatch" {
  name = "my-cloudwatch"
  cloudwatch {
    region = "us-east-1"
    group  = local.cloudwatch_log_group_name
    stream = "cyral-sidecar"
  }
}

resource "cyral_sidecar_credentials" "creds" {
  sidecar_id = cyral_sidecar.sidecar.id
}

module "cyral_sidecar" {
  source  = "cyralinc/sidecar-ec2/aws"
  version = "~> 4.0"

  sidecar_id      = cyral_sidecar.sidecar.id

  cloudwatch_log_group_name = local.cloudwatch_log_group_name

  client_id = cyral_sidecar_credentials.creds.client_id
  client_secret = cyral_sidecar_credentials.creds.client_secret

  ...
}
```

### Custom Integration

Advanced users can use the "Fluent Bit" logging integration to customize how the sidecar sends logs to
their log management destination. Using the `fluent_bit` block, users can provide a customized Fluent Bit
configuration (in ["classic mode" INI format](https://docs.fluentbit.io/manual/administration/configuring-fluent-bit/classic-mode)),
using any combination of filter and output plugins. More information can be found in the official
Fluent Bit [documentation](https://docs.fluentbit.io/manual/concepts/data-pipeline) (see the "Filter"
and "Output" sections).

```terraform
# Configures `my-sidecar-fluent-bit` to push logs to a bucket named
# `example-bucket` in AWS S3.
resource "cyral_sidecar" "sidecar_fluent_bit" {
  name               = "my-sidecar-fluent-bit"
  deployment_method  = "terraform"
  log_integration_id = cyral_integration_logging.s3.id
}

resource "cyral_integration_logging" "s3" {
  name = "my-s3"
  fluent_bit {
    config = <<-EOF
    [OUTPUT]
      Name s3
      Match *
      Region us-east-2
      Bucket example-bucket
      Total_file_size 1M
    EOF
  }
}

# Configures a raw Elk integration with no sidecar associated.
resource "cyral_integration_logging" "elk_integration" {
  name = "my-elk-integration"
  elk {
    es_url     = "http://es.com"
    kibana_url = "http://kibana.com"
    es_credentials {
      username = "another-user"
      password = "123"
    }
  }
}
```

<!-- schema generated by tfplugindocs -->

## Schema

### Required

- `name` (String) Name of the logging integration config.

### Optional

- `cloudwatch` (Block Set, Max: 1) Represents the configuration data required for the `AWS` CloudWatch log management system. (see [below for nested schema](#nestedblock--cloudwatch))
- `datadog` (Block Set, Max: 1) Represents the configuration data required for the Datadog's log management system. (see [below for nested schema](#nestedblock--datadog))
- `elk` (Block Set, Max: 1) Represents the configuration data required for the ELK stack log management system. (see [below for nested schema](#nestedblock--elk))
- `fluent_bit` (Block Set, Max: 1) Represents a custom Fluent Bit configuration which will be utilized by the sidecar's log shipper. (see [below for nested schema](#nestedblock--fluent_bit))
- `receive_audit_logs` (Boolean) Whether or not Cyral audit logs should be forwarded to this logging integration. Declaration not supported in conjunction with `fluent_bit` block.
- `splunk` (Block Set, Max: 1) Represents the configuration data required for the Splunk log management system. (see [below for nested schema](#nestedblock--splunk))
- `sumo_logic` (Block Set, Max: 1) Represents the configuration data required for the Sumo Logic log management system. (see [below for nested schema](#nestedblock--sumo_logic))

### Read-Only

- `id` (String) Unique identifier of the logging integration.

<a id="nestedblock--cloudwatch"></a>

### Nested Schema for `cloudwatch`

Required:

- `group` (String) CloudWatch log group.
- `region` (String) AWS region.

Optional:

- `stream` (String) CloudWatch log stream. Defaults to `cyral-sidecar` if not set.

<a id="nestedblock--datadog"></a>

### Nested Schema for `datadog`

Required:

- `api_key` (String, Sensitive) DataDog API key.

<a id="nestedblock--elk"></a>

### Nested Schema for `elk`

Required:

- `es_url` (String) Elasticsearch URL.

Optional:

- `es_credentials` (Block Set, Max: 1) Credentials used to authenticate to Elastic Search (see [below for nested schema](#nestedblock--elk--es_credentials))
- `kibana_url` (String) Kibana URL.

<a id="nestedblock--elk--es_credentials"></a>

### Nested Schema for `elk.es_credentials`

Required:

- `password` (String, Sensitive) Elasticsearch password.
- `username` (String) Elasticsearch username.

<a id="nestedblock--fluent_bit"></a>

### Nested Schema for `fluent_bit`

Required:

- `config` (String) Fluent Bit configuration, in 'classic mode' INI format. For more details, see: https://docs.fluentbit.io/manual/administration/configuring-fluent-bit/classic-mode/configuration-file

<a id="nestedblock--splunk"></a>

### Nested Schema for `splunk`

Required:

- `access_token` (String, Sensitive) Splunk access token.
- `hec_port` (String) Splunk HTTP Event Collector (HEC) port.
- `hostname` (String) Splunk hostname.

Optional:

- `index` (String) Splunk index which logs should be indexed to.
- `use_tls` (Boolean) Whether or not to use TLS.

<a id="nestedblock--sumo_logic"></a>

### Nested Schema for `sumo_logic`

Required:

- `address` (String) Sumo Logic HTTP collector address. A full URL is expected
